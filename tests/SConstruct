import os
import sys

ENABLE_QT_TEST = False

if sys.platform.startswith('win'):
    CCFLAGS = ['/EHsc', '/nologo']
    LIBS = []
else:
    CCFLAGS = ''
    LIBS = ['pthread']

env = Environment(
    CPPPATH=os.environ.get('INCLUDE', []),
    CCFLAGS=CCFLAGS,
    LIBPATH=os.environ.get('LIBPATH', []),
    LIBS=LIBS,
)

# google test env
genv = env.Clone(LIBS=['gtest'] + LIBS)

if ENABLE_QT_TEST:
    # qt5 env
    QT5DIR = "/opt/Qt5.7.0/5.7/gcc_64"
    qtenv = env.Clone(QT5DIR=QT5DIR,
                      CCFLAGS="-std=c++11 -fPIC",
                      tools=['default','qt5'])
    qtenv['ENV']['PKG_CONFIG_PATH'] = os.path.join(QT5DIR, 'lib/pkgconfig')
    qtenv.EnableQt5Modules([
                        'QtTest'
                       ])
    Export('qtenv')

Export('env genv')

# build google test target
genv.Program('gtest.cpp')

# build boost target
for filename in ('boost_success.cpp', 'boost_failure.cpp', 'boost_error.cpp'):
    env.Program(filename)

# build qt5 target
if ENABLE_QT_TEST:
    for filename in ('qt_success.cpp', 'qt_failure.cpp', 'qt_error.cpp'):
        qtenv.Program(filename)

SConscript('acceptance/googletest-samples/SConscript')
SConscript('acceptance/boosttest-samples/SConscript')
